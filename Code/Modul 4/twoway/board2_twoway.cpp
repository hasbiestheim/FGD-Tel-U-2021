// Complete Instructions to Get and Change ESP MAC Address: https://RandomNerdTutorials.com/get-change-esp32-esp8266-mac-address-arduino/
#include <Arduino.h>

#include <esp_now.h>
#include <WiFi.h>

#include <Wire.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BMP280.h>


Adafruit_BMP280 bmp;

// REPLACE WITH THE MAC Address of your receiver 
uint8_t broadcastAddress[] = {0x94, 0xB9, 0x7E, 0xF9, 0x93, 0xB4};

// Define variables to store BME280 readings to be sent
float temperature;
float pressure;

// Define variables to store incoming readings
float incomingTemp;
float incomingPres;

// Define board id
uint8_t boardid = 2;
uint8_t incomingid;

// Variable to store if sending data was successful
String success;

//Structure example to send data
//Must match the receiver structure
typedef struct struct_message {
    uint8_t id;
    float temp;
    float pres;
} struct_message;

// Create a struct_message called BME280Readings to hold sensor readings
struct_message BMP280Readings;

// Create a struct_message to hold incoming sensor readings
struct_message incomingReadings;

// Callback when data is sent
void OnDataSent(const uint8_t *mac_addr, esp_now_send_status_t status) {
  Serial.print("\r\nLast Packet Send Status:\t");
  Serial.println(status == ESP_NOW_SEND_SUCCESS ? "Delivery Success" : "Delivery Fail");
  if (status ==0){
    success = "Delivery Success :)";
  }
  else{
    success = "Delivery Fail :(";
  }
}

// Callback when data is received
void OnDataRecv(const uint8_t * mac, const uint8_t *incomingData, int len) {
  memcpy(&incomingReadings, incomingData, sizeof(incomingReadings));
  Serial.print("Bytes received: ");
  Serial.println(len);
  incomingid = incomingReadings.id;
  incomingTemp = incomingReadings.temp;
  incomingPres = incomingReadings.pres;
}

void getReadings();
void Update();

void setup() {
  // Init Serial Monitor
  Serial.begin(115200);
  bmp.begin(0x76);

  /* Default settings from datasheet. */
  bmp.setSampling(Adafruit_BMP280::MODE_NORMAL,     /* Operating Mode. */
                  Adafruit_BMP280::SAMPLING_X2,     /* Temp. oversampling */
                  Adafruit_BMP280::SAMPLING_X16,    /* Pressure oversampling */
                  Adafruit_BMP280::FILTER_X16,      /* Filtering. */
                  Adafruit_BMP280::STANDBY_MS_500); /* Standby time. */
  // Set device as a Wi-Fi Station
  WiFi.mode(WIFI_STA);
  Serial.println(WiFi.macAddress());
  
  // Init ESP-NOW
  if (esp_now_init() != ESP_OK) {
    Serial.println("Error initializing ESP-NOW");
    return;
  }


  // Once ESPNow is successfully Init, we will register for Send CB to
  // get the status of Trasnmitted packet
  esp_now_register_send_cb(OnDataSent);
  
  // Register peer
  esp_now_peer_info_t peerInfo;
  memcpy(peerInfo.peer_addr, broadcastAddress, 6);
  peerInfo.channel = 0;  
  peerInfo.encrypt = false;
  
  // Add peer        
  if (esp_now_add_peer(&peerInfo) != ESP_OK){
    Serial.println("Failed to add peer");
    return;
  }
  // Register for a callback function that will be called when data is received
  esp_now_register_recv_cb(OnDataRecv);
  Serial.println("Done setup");
}
 
void loop() {
  getReadings();
 
  // Set values to send
  BMP280Readings.id = boardid;
  BMP280Readings.temp = temperature;
  BMP280Readings.pres = pressure;

  // Send message via ESP-NOW
  esp_err_t result = esp_now_send(broadcastAddress, (uint8_t *) &BMP280Readings, sizeof(BMP280Readings));
   
  if (result == ESP_OK) {
    Serial.println("Sent with success");
  }
  else {
    Serial.println("Error sending the data");
  }
  Update();
  delay(10000);
}
void getReadings(){

  temperature = bmp.readTemperature();
  pressure = (bmp.readPressure() / 1013.25);
}

void Update(){
  
  // Display Readings in Serial Monitor
  Serial.println("INCOMING READINGS From board:");
  Serial.println(incomingReadings.id);
  Serial.print("Temperature: ");
  Serial.print(incomingReadings.temp);
  Serial.println(" ÂºC");
  Serial.print("Pressure: ");
  Serial.print(incomingReadings.pres);
  Serial.println(" hPa");
  Serial.println();
}